plugins {
    id 'org.springframework.boot' version '2.6.4' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
    id 'com.google.cloud.tools.jib' version '3.2.0' apply false
}

allprojects {
    group = 'ua.tunepoint.account'
    version = '0.0.1'
}

subprojects {

    apply plugin: 'java-library'
    apply plugin: "io.spring.dependency-management"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        repositories {
            maven {
                url "http://artifactory.tunepoint.fun/releases"
                allowInsecureProtocol true
            }
        }
        mavenCentral()
    }

    dependencyManagement {
        dependencies {

            dependency 'org.springframework.cloud:spring-cloud-starter-openfeign:3.1.1'
            dependency 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:3.1.1'
            dependency 'org.springframework.boot:spring-boot-starter-data-jpa:2.6.4'
            dependency 'org.springframework.boot:spring-boot-starter-security:2.6.4'
            dependency 'org.springframework.boot:spring-boot-starter-validation:2.6.4'
            dependency 'org.springframework.boot:spring-boot-starter-web:2.6.4'

            dependency 'org.projectlombok:lombok:1.18.22'

            dependency 'org.postgresql:postgresql:42.3.3'

            dependency 'org.springframework.boot:spring-boot-starter-test:2.6.4'
            dependency 'org.springframework.security:spring-security-test:5.6.2'

            dependency "org.mapstruct:mapstruct:1.4.2.Final"
            dependency "org.mapstruct:mapstruct-processor:1.4.2.Final"

            dependency 'org.liquibase:liquibase-core:4.9.0'

            dependency 'com.auth0:java-jwt:3.19.0'

            dependency 'org.hibernate.validator:hibernate-validator:6.2.2.Final'

            dependency 'ua.tunepoint:commons-security:0.0.1'
            dependency 'ua.tunepoint:resource-api:0.0.1'
            dependency 'ua.tunepoint:resource-model:0.0.1'
        }
    }

    if (it.ext.has('publish')) {
        apply plugin: 'maven-publish'

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    groupId project.group
                    artifactId project.name
                    version project.version

                    artifact jar
                }
            }

            repositories {
                maven {
                    name  'reposilite'
                    url "http://artifactory.tunepoint.fun/${System.getenv('ARTIFACTORY_REPOSITORY')}"
                    allowInsecureProtocol true

                    credentials {
                        username System.getenv('ARTIFACTORY_USERNAME')
                        password System.getenv('ARTIFACTORY_PASSWORD')
                    }
                }
            }
        }
    }
}